// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    // is a_instruction or not
    Mux16(a=instruction, b=ALUOut, sel=instruction[15], out=MuxAOut) ;
    
    // a register
    Mux(a=true, b=instruction[5], sel=instruction[15], out=ARegisterLoad) ;
    ARegister(in=MuxAOut, load=ARegisterLoad, out=ARegisterOut, out[0..14]=addressM) ;
    
    // d register
    Mux(a=false, b=instruction[4], sel=instruction[15], out=DRegisterLoad) ;
    DRegister(in=ALUOut, load=DRegisterLoad, out=DRegisterOut) ;
    
    // compute
    Mux16(a=ARegisterOut, b=inM, sel=instruction[12], out=MuxALUOut) ;
    ALU(x=DRegisterOut, y=MuxALUOut, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=ALUOut, out=outM, zr=ALUZr, ng=ALUNg) ;
    
    //write M or not
    Mux(a=false, b=instruction[3], sel=instruction[15], out=writeM) ;
    
    // jump result
    Or(a=ALUNg, b=ALUZr, out=NgOrZr) ;
    Not(in=NgOrZr, out=positive) ;
    And(a=ALUNg, b=instruction[2], out=ngAndInst2) ;
    And(a=ALUZr, b=instruction[1], out=zrAndInst1) ;
    And(a=positive, b=instruction[0], out=positiveAndInst0) ;
    Or(a=ngAndInst2, b=zrAndInst1, out=ngAndInst2OrZrAndInst1) ;
    Or(a=ngAndInst2OrZrAndInst1, b=positiveAndInst0, out=jumpResult1) ;
    
    // PC
    And(a=instruction[15], b=jumpResult1, out=jumpResult2) ;
    PC(in=ARegisterOut, load=jumpResult2, inc=true, reset=reset, out[0..14]=pc) ;
}